apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.googlecode.json-simple'
version = '1.2.0'

description = "JSON.simple - A simple Java toolkit for JSON"

sourceCompatibility = 1.5
targetCompatibility = 1.5

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

manifest {
    attributes(
            "Bundle-Name": "JSON.simple",
            "Bundle-SymbolicName": "com.googlecode.json-simple.json-simple",
            "Bundle-Version": "${project.version}",
            "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
            "Bundle-Description": "A simple Java toolkit for JSON",
            "Export-Package": "org.json.simple, org.json.simple.parser"
    )
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url 'http://heisluft.bplaced.net/upload/'
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Yidong'
                        name = 'Yidong Fang'
                        email = 'fangyidong@yahoo.com.cn'
                    }
                    developer {
                        id = 'heisluft'
                        name = 'heisluft'
                        email = 'heisluftlp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/heisluft/json-simple'
                    developerConnection = 'scm:git:https://github.com/heisluft/json-simple'
                    url = 'https://github.com/heisluft/json-simple'
                }
            }
        }
    }
}

publish.dependsOn test

repositories {
    mavenCentral()
}

signing {
    sign publishing.publications.maven
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
